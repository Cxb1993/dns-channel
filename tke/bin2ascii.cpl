! This program converst the output tke.bin of tke.cpl
! into several human-readable ASCII files that represents
! the energy box. This is needed because ASCII writing is
! typically done serially.

gamma=0
outinterv=10
USE ../dnsdata
USE ../dnsdirect
USE rtchecks
USE convenience

! Definitions
! ------------------------------------
MEANTERMS = STRUCTURE(REAL U,W,Uy,Wy,Uyy,Wyy)
RMS = STRUCTURE(REAL uu,vv,ww,uv,uw,vw)
TKETERMS = STRUCTURE(REAL PRuv,PRvw,PDISS,TDIFF,PDIFF,VDIFF,PHI_TDIFF,PHI_PDIFF,PHI_VDIFF)
MKETERMS = STRUCTURE(REAL MPROD,TPRuv,TPRvw,TMDIF,MMDIF,DISSu,DISSw,PHI_TMDIF,PHI_MMDIF)
POINTER TO STORED STRUCTURE[
        ARRAY(-1..ny+1) OF MEANTERMS meandataimage
        ARRAY(-1..ny+1) OF TKETERMS tkedataimage
        ARRAY(-1..ny+1) OF MKETERMS mkedataimage
        ARRAY(-1..ny+1) OF RMS rmsdataimage
] tkeimage

! Write to ASCII file
! ------------------------------------
tkeimage = OPEN("tke.bin")
! TKE
FILE outfile=CREATE("tke.dat")
WRITE TO outfile "y","PRuv","PRvw","PDISS","TDIFF","PDIFF","VDIFF","PHI_TDIFF","PHI_PDIFF","PHI_VDIFF"
DO WITH tkeimage.tkedataimage(iy): WRITE TO outfile y(iy),PRuv,PRvw,PDISS,TDIFF,PDIFF,VDIFF,PHI_TDIFF,PHI_PDIFF,PHI_VDIFF FOR iy=0 TO ny
CLOSE(outfile)
! TKE - integrals
FILE outfile=CREATE("tkeintegrals.dat")
WRITE TO outfile "PRuv","PRvw","PDISS","TDIFF","PDIFF","VDIFF"
WITH tkeimage.tkedataimage: WRITE TO outfile yintf(PRuv),yintf(PRvw),yintf(PDISS),yintf(TDIFF),yintf(PDIFF),yintf(VDIFF)
CLOSE(outfile)
! MKE
FILE outfile=CREATE("mke.dat")
WRITE TO outfile "y","MPROD","TPRuv","TPRvw","TMDIF","MMDIF","DISSu","DISSw","PHI_TMDIF","PHI_MMDIF"
DO WITH tkeimage.mkedataimage(iy): WRITE TO outfile y(iy),MPROD,TPRuv,TPRvw,TMDIF,MMDIF,DISSu,DISSw,PHI_TMDIF,PHI_MMDIF FOR iy=0 TO ny
CLOSE(outfile)
! MKE - integrals
FILE outfile=CREATE("mkeintegrals.dat")
WRITE TO outfile "MPROD","TPRuv","TPRvw","TMDIF","MMDIF","DISSu","DISSw"
WITH tkeimage.mkedataimage: WRITE TO outfile yintf(MPROD),yintf(TPRuv),yintf(TPRvw),yintf(TMDIF),yintf(MMDIF),yintf(DISSu),yintf(DISSw)
CLOSE(outfile)
! RMS
FILE outfile=CREATE("rms.dat")
WRITE TO outfile "y","uu","vv","ww","uv","uw","vw"
DO WITH tkeimage.rmsdataimage(iy): WRITE TO outfile y(iy),uu,vv,ww,uv,uw,vw FOR iy=0 TO ny
CLOSE(outfile)
! MEAN
FILE outfile=CREATE("mean.dat")
WRITE TO outfile "y","U","W","Uy","Wy","Uyy","Wyy"
DO WITH tkeimage.meandataimage(iy): WRITE TO outfile y(iy),U,W,Uy,Wy,Uyy,Wyy FOR iy=0 TO ny
CLOSE(outfile)
CLOSE(tkeimage)
